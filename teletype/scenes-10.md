---
layout: default
title: scenes
parent: teletype
nav_order: 3
redirect_from: /modular/teletype/scenes-10
---

# Teletype 1.0 Scenes

Direct downloads:
* [tt00.txt](../tt00.txt)
* [tt01.txt](../tt01.txt)
* [tt02.txt](../tt02.txt)
* [tt03.txt](../tt03.txt)
* [tt04.txt](../tt04.txt)
* [tt05.txt](../tt05.txt)
* [tt06.txt](../tt06.txt)
* [tt07.txt](../tt07.txt)

## 0 TRIANGLE MOUNTAIN

TEMPO ON METRO, SET WITH PARAM KNOB

CV 1 TO OSC FREQ

* 1: RESET POSITION TO 0
* 2: NEXT SCALE
* 3: SHORTEN LOOP
* 4: TOGGLE FORWARD/REVERSE
* 5: THROW CV 2 A NEW NOTE
* 6: RANDOMIZE LOOP LENGTH
* 7: SET ROOT TO CV IN
* 8: ADD 1 SEMI TO CV 4

CHANGE SCALES IN TRACKER.

PATCH CV 2 TO ANOTHER OSC FOR WEIRD COUNTERPOINT

TRY PATCHING CV4 TO CV IN FOR AUTOMATIC TRANSPOSITION.

~~~
1:  P.I 0

2:  P.N WRAP ADD P.N 1 0 3
    P.I P.END

3:  P.END WRAP SUB P.END 1 1 7

4:  X EZ X

5:  S : CV 2 ADD N P.HERE V 1
    S : TR.PULSE B

6:  P.END RRAND 1 7

7:  Z IN

8:  CV 4 N WRAP 0 0 11

M:  IF X : P.PREV
    ELSE : P.NEXT
    CV 1 ADD N P.HERE Z
    M SUB 320 RSH PARAM 6
    S.ALL
    TR. PULSE A

I:  L A B : TR.TIME I 40
    RE 4 1
~~~

## 1 RANDOMS

INS CORRESPOND TO OUTPUT JACKS BENEATH:

* 1: RANDOM STATE (TR A)
* 2: RAND DIMINISHED NOTES (CV1)
* 3: BURST GEN (TR B)
* 4: DRUNK SLEWED VOLTS (CV2)
* 5: START/RESET LFO
* 6: 1/F MELODY GENERATOR (CV3)
* 7: (FROM TR D)
* 8: (FROM TR C)

PATCH TR C TO INS, AND TR D TO IN7 FOR LFO FUNCTIONS.

PARAM KNOB SETS BURST RATE.

~~~
1:  TR A TOSS

2:  CV 1 N QT RAND 36 3

3:  Y ADD 200 RSH PARAM 4
    TR.PULSE B
    DEL RAND Y : TR.PULSE B
    DEL RAND Y : TR.PULSE B
    DEL RAND Y : TR.PULSE B
    DEL ADD 40 Y : TR B 0

4:  Z V RRAND -2 2
    CV 2 ADD CV 2 Z

5:  T RRAND 10 10000
    CV.SET 4 0
    CV.SLEW 4 T
    CV 4 V 8
    DEL T : TR.PULSE C

6:  X ADD X RRAND -1 1
    PROB 50 : X ADD X RRAND -2 2
    PROB 25 : X ADD X RRAND -4 4
    PROB 12 : X ADD X RRAND -8 8
    PROB 6 : X ADD X RRAND -16 16
    CV 3 N WRAP X 0 48

7:  T RRAND 10 10000
    CV.SLEW 4 Y
    CV 4 V 8
    DEL T : TR.PULSE C

8:  T RRAND 10 10000
    CV.SLEW 4 T
    CV 4 0
    DEL T : TR.PULSE D

M:

I:  L A B : TR.TIME I 25
    DRUNK 12
    CV.SLEW 2 1000
    TR.PULSE D
    CV.SLEW 3 5
~~~

## 2 HIDDEN PATH NO PATH

CV 1: OSC FREQ (MAIN)
CV 2: OSC FREQ (DRONE)
TR 1-4: PROBABLE GATES

* 1: CLOCK
* 2: WANDERING STEP
* 3: CV 2 MIRRO CV 1
* 4: STEP FORWARD
* 5: GENTLE WANDER CV 3&4
* 6: JUMP TO START
* 7: SUDDEN TELEPORT CV 3&4

~~~
1:  S.ALL
    CV 1 N P.HERE
    TR.PULSE A
    PROB 80 : TR.PULSE B
    PROB 50 : TR.PULSE C
    PROB 20 : TR.PULSE D

2:  IF TOSS : P.NEXT
    ELIF TOSS : P.PREV

3:  S : CV 2 CV 1

4:  P.NEXT

5:  CV 3 V RAND 5
    CV 4 V RAND TOSS

6:  P.I 0

7:  CV.SET 3 V RAND 5
    CV.SET 4 V RAND TOSS

8:

M:

I:  CV.SLEW 3 500
    CV.SLEW 4 200
    L 1 4 : TR I 0
    P.N 0
~~~

## 3 4TRACK

NUMLOCK ENTERS TRACKER MODE TO EDIT NOTES
COLS 1-3 ARE NOTES, 4 IS CV 0-1000 FOR OV-10.00V CV/TR OUT PER COLUMN

* 1-4: CLOCK INDIVIDUAL TRACK
* 5: CLOCK ALL
* 6: RESET
* 7: DRUNK POSITION

~~~
1:  P.N 0
    CV 1 N P.NEXT
    TR.PULSE A

2:  P.N 1
    CV 2 N P.NEXT
    TR.PULSE B

3:  P.N 2
    CV 3 N P.NEXT
    TR.PULSE C

4:  P.N 3
    CV 4 VV P.NEXT
    TR.TOG D

5:  P.N 0
    CV 1 N P.NEXT
    CV 2 N PN 1 P.HERE
    CV 3 N PN 2 P.HERE
    CV 4 N PN 3 P.HERE
    L A D : TR.PULSE I

6:  P.I P.END

7:  P.I ADD P.I RRAND -2 1

8:

M:

I:  L A D : TR.TIME 30
    L 1 4 : CV.SLEW 50
~~~

## 4   MOOD RING

3 VOICE JAZZ BOX GENERATOR

CV 1-3 TO VCO PITCH
TR A-C TO ENVELOPES

TR 1-4 CHANGE ROOT NOTE

* 1: DOWN 2ND
* 2: UP 2ND
* 3: DOWN 3RD
* 4: UP 4TH

TR 5-8 IMPROVISE SCALES

* 5: DOWN 2ND
* 6: UP 2ND
* 7: UP 3RD
* 8: UP 4TH

TR 1 HAS A RHYTHM SET BY METRO TIME.

DRIVE TR1-4 WITH A SLOW CLOCK OR MANUALLY WITH WALK.
DRIVE TR5-8 WITH A FAST CLOCK OR MULTIPLE CLOCK UNPUTS FOR ALGORITHMIC SOLOING.

~~~
1:  X WRAP SUB X 1 0 6
    S : CV 1 N PN 0 X
    IF EQ X 6 : P.N 3
    ELIF EQ X 4 : P.N 2
    ELIF EQ 0 MOD X 3 : P.N 0
    ELSE : P.N 1

2:  X WRAP ADD X 1 0 6
    S : CV 1 N PN 0 X
    IF EQ X 6 : P.N 3
    ELIF EQ X 4 : P.N 2
    ELIF EQ 0 MOD X 3 : P.N 0
    ELSE : P.N 1

3:  X WRAP SUB X 2 0 6
    S : CV 1 N PN 0 X
    IF EQ X 6 : P.N 3
    ELIF EQ X 4 : P.N 2
    ELIF EQ 0 MOD X 3 : P.N 0
    ELSE : P.N 1

4:  X WRAP ADD X 3 0 6
    S : CV 1 N PN 0 X
    IF EQ X 6 : P.N 3
    ELIF EQ X 4 : P.N 2
    ELIF EQ 0 MOD X 3 : P.N 0
    ELSE : P.N 1

5:  Y PN 0 X
    P.I WRAP SUB P.I 1 0 14
    CV 3 N ADD Y P.HERE
    Z ADD 2 MUL 4 TOSS
    CV 2 N PN P.N ADD Z X
    PROB 70 : TR.PULSE 3

6:  Y PN 0 X
    P.I WRAP ADD P.I 1 0 14
    CV 3 N ADD Y P.HERE
    Z ADD 2 MUL 4 TOSS
    CV 2 N ADD Y PN P.N Z
    PROB 70 : TR.PULSE 3

7:  Y PN 0 X
    P.I WRAP ADD P.I 2 0 14
    CV 3 N ADD Y P.HERE
    Z ADD 2 MUL 4 TOSS
    CV 2 N ADD Y PN P.N Z
    PROB 70 : TR.PULSE 3

8:  Y PN 0 X
    P.I WRAP ADD P.I 3 0 14
    CV 3 N ADD Y P.HERE
    Z ADD 2 MUL 4 TOSS
    CV 2 N ADD Y PN P.N Z
    PROB 70 : TR.PULSE 3

M:  S.POP
    PROB 50 : TR.PULSE A
    DEL MUL T RRAND 1 5 : TR.TOG A
    DEL MUL T 4 : TR.PULSE
    DEL MUL T 7 : TR.PULSE A
    PROB 80 : TR.PULSE B

I:  M 750
    T 93
    L A D : TR.TIME 25
    L 1 4 : CV.SLEW I 12
~~~

## 5 WHITE WHALE REMOTE

MUST CONNECT VIA CABLE

* 1: PRESET 0
* 2: NEXT PRESET
* 3: HARD SYNC, CUT TO KNOB POS
* 4: CUT TO KNOB POS
* 5: PATTERN 0
* 6: QUEUED PATTERN TO KNOB POS
* 7: MUTE TRIGGERS
* 8: UNMUTE TRIGGERS

FOR KNOB SCRIPTS, TRY CHANGING PARAM TO IN,
FOR CV CONTROL!

~~~
1:  X 0
    II WW.PRESET X

2:  X WRAP ADD X 1 0 7
    II WW.PRESET X

3:  X RSH PARAM 10
    II WW.SYNC X

4:  X RSH PARAM 10
    II WW.POS X

5:  II WW.PATTERN 0

6:  X RSH PARAM 10
    II WW.QPATTERN X

7:  II WW.MUTE1 0
    II WW.MUTE2 0
    II WW.MUTE3 0
    II WW.MUTE4 0

8:  II WW.MUTE1 1
    II WW.MUTE2 1
    II WW.MUTE3 1
    II WW.MUTE4 1

M:

I:
~~~

## 6 MEADOWPHYSICS REMOTE

MUST CONNECT VIA CABLE

* 1: PRESET 0
* 2: NEXT PRESET
* 3: RESET COUNT
* 4: HARD SYNC
* 5: FREEZE 1
* 6: UNFREEZE 1
* 7: MUTE ALL
* 8: UNMUTE ALL

~~~
1:  X 0
    II MP.PRESET X

2:  X WRAP ADD X 1 0 7
    II MP.PRESET X

3:  II MP.RESET 1

4:  II MP.SYNC 1

5:  II MP.FREEZE 1

6:  II MP.UNFREEZE 1

7:  L 1 8 : II MP.MUTE I

8:  L 1 8 : II MP.UNMUTE I

M:

I:
~~~

## 7 EARTHSEA REMOTE

MUST CONNECT VIA CABLE

* 1: PRESET 0
* 2: NEXT PRESET
* 3: RESET/PLAY PATTERN
* 4: STOP PATTERN
* 5: CHANGE PATTERN TO KNOB
* 6: SET TRANSPOSITION TO KNOB
* 7: TOGGLE CLOCK MODE
* 8: EXT CLOCK

~~~
1:  X 0
    II ES.PRESET X

2:  X WRAP ADD X 1 0 7
    II ES.PRESET X

3:  II ES.RESET 1

4:  II ES.STOP 1

5:  X RSH PARAM 10
    II ES.PATTERN X

6:  X RSH PARAM 10
    II ES.TRANS X

7:  Z EZ Z
    II ES.MODE Z

8:  II ES.CLOCK 1

M:

I:
~~~
